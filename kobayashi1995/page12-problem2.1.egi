;;
;; (問2.1) x = x(t), y = y(t) で与えられた曲線の曲率 κ を計算する．
;;

(define $x (function [t]))
(define $y (function [t]))
(define $p [| x y |])
(define $p' (d/d p t))
(define $e1 (/ p' (sqrt (V.* p' p'))))
(define $e2 [| (* -1 e1_2) e1_1 |])

e1 ; [| (/ x|t (sqrt (+ x|t^2 y|t^2))) (/ y|t (sqrt (+ x|t^2 y|t^2))) |]
e2 ; [| (/ (* -1 y|t) (sqrt (+ x|t^2 y|t^2))) (/ x|t (sqrt (+ x|t^2 y|t^2))) |]

(define $e1' (d/d e1 t))

e1'
;[|(/ (+ (* y|t^2 x|t|t)
;        (* -1 y|t y|t|t x|t))
;     (+ (* (sqrt (+ x|t^2 y|t^2)) x|t^2)
;        (* (sqrt (+ x|t^2 y|t^2)) y|t^2)))
;  (/ (+ (* x|t^2 y|t|t)
;        (* -1 x|t x|t|t y|t))
;     (+ (* (sqrt (+ x|t^2 y|t^2)) x|t^2)
;        (* (sqrt (+ x|t^2 y|t^2)) y|t^2))) |]

(define $ds/dt (sqrt (V.* p' p')))
(define $dt/ds (/ 1 ds/dt))

dt/ds ; (/ 1 (sqrt (+ x|t^2 y|t^2)))

(define $κ (* (/ e1'_1 e2_1) dt/ds))

κ
;(/ (+ (* y|t x|t|t)
;      (* -1 y|t|t x|t))
;   (+ (* -1 x|t^2 (sqrt (+ x|t^2 y|t^2)))
;      (* -1 y|t^2 (sqrt (+ x|t^2 y|t^2)))))

;;
;; Exercise. 上記のプログラムを参考に12ページの「問2.2」のための計算をするプログラムを書け．
;; (問2.2) 曲線が極座標 (r, θ) を使って r = F(θ) と与えられているときの曲率 κ を計算せよ．
;;