;;
;; (問題2.1) 曲面 z = f(x, y) のGaussの曲率 K = κ₁ κ₂ と平均曲率 H = (κ₁ + κ₂) / 2 を求めよ．
;;

(define $f (function [x y]))

(define $v1 [| 1 0 (∂/∂ f x) |])
(define $v2 [| 0 1 (∂/∂ f y) |])

v1 ; [| 1 0 f|x |]
v2 ; [| 0 1 f|y |]

(define $v3 (cross-product v1 v2))

v3 ; [| (* -1 f|x) (* -1 f|y) 1 |]

(define $e3 (/ v3 (sqrt '(V.* v3 v3))))

e3 ; [| (/ (* -1 f|x) (sqrt '(+ f|x^2 f|y^2 1))) (/ (* -1 f|y) (sqrt '(+ f|x^2 f|y^2 1))) (/ 1 (sqrt '(+ f|x^2 f|y^2 1))) |]

(define $E (V.* v1 v1))
(define $F (V.* v1 v2))
(define $G (V.* v2 v2))

E ; (+ 1 f|x^2)
F ; (* f|x f|y)
G ; (+ 1 f|y^2)

(define $L (V.* (∂/∂ v1 x) e3))
(define $M (V.* (∂/∂ v1 y) e3))
;(define $M (V.* (∂/∂ v2 x) e3))
(define $N (V.* (∂/∂ v2 y) e3))

L ; (/ f|x|x (sqrt '(+ f|x^2 f|y^2 1)))
M ; (/ f|x|y (sqrt '(+ f|x^2 f|y^2 1)))
N ; (/ f|y|y (sqrt '(+ f|x^2 f|y^2 1)))

(define $K (/ (- (* L N) (** M 2))
              '(- (* E G) (** F 2))))
(define $H (/ (+ (* 'E N) (* 'G L) (* -2 F M))
              (* 2 '(- (* E G) (** F 2)))))

K
;(/ (+ (* f|x|x f|y|y)
;      (* -1 f|x|y^2))
;   '(+ f|x^2 f|y^2 1)^2)

H
;(/ (+ (* '(+ 1 f|x^2) f|y|y)
;      (* '(+ 1 f|y^2) f|x|x)
;      (* -2 f|x f|y f|x|y))
;   (* 2 (sqrt '(+ f|x^2 f|y^2 1)) '(+ 1 f|y^2 f|x^2)))
